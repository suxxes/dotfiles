# chezmoi:template:left-delimiter="# {{" right-delimiter="}}"
# Secrets auto-loader

# {{ if eq .machineType "mac" -}}
# Mac: Load directly from 1Password on demand
function __load_op_secrets
    if not command -q op
        return
    end

    # Get all items from Secrets vault
    set -l items (op item list --vault Secrets --format json 2>/dev/null | jq -r '.[].title' 2>/dev/null)

    for title in $items
        # Skip service account and LXC-specific items
        if string match -q "Service Account Token" $title
            continue
        end

        if string match -q "LXC:*" $title
            continue
        end

        # Get fields and export
        op item get $title --vault Secrets --format json 2>/dev/null | \
            jq -r '.fields[]? | select(.value != null and .value != "") | select(.id != "notesPlain") | "\(.label)\t\(.value)"' 2>/dev/null | \
            while read -l key value
                set -l clean_key (string upper $key | string replace -a ' ' '_' | string replace -a '-' '_')
                set -gx $clean_key $value
            end
    end
end

# Load on shell start (cached for session)
if status is-interactive
    __load_op_secrets
end

# {{ else -}}
# Linux/LXC: Load from generated file
if test -f ~/.config/secrets/env.fish
    source ~/.config/secrets/env.fish
end
# {{ end }}
